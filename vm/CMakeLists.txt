cmake_minimum_required(VERSION 2.8.8)
project(build)
include(CheckCCompilerFlag)
include(ExternalProject)

enable_testing()

if(NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE "Debug")
endif()

if(NOT ARCH)
  message(FATAL_ERROR "ARCH not set")
endif()
if(NOT ARCH MATCHES "^(x86|armv6|armv7|thumbv6|thumbv7)$")
  message(FATAL_ERROR "Unsupported ARCH: ${ARCH}")
endif()
if(NOT OS)
  message(FATAL_ERROR "OS not set")
endif()
if(NOT OS MATCHES "^(linux|macosx|ios)$")
  message(FATAL_ERROR "Unsupported OS: ${OS}")
endif()

if(OS STREQUAL "linux")
  set(LINUX YES)
  set(OS_FAMILY linux)
elseif(OS STREQUAL "macosx")
  set(MACOSX YES)
  set(DARWIN YES)
  set(OS_FAMILY darwin)
elseif(OS STREQUAL "ios")
  set(IOS YES)
  set(DARWIN YES)
  set(OS_FAMILY darwin)
endif()

if (ARCH STREQUAL "x86")
  set(X86 YES)
elseif (ARCH STREQUAL "armv6")
  set(ARM YES)
  set(ARMV6 YES)
elseif (ARCH STREQUAL "armv7")
  set(ARM YES)
  set(ARMV7 YES)
elseif (ARCH STREQUAL "thumbv6")
  set(ARM YES)
  set(THUMB YES)
  set(THUMBV6 YES)
elseif (ARCH STREQUAL "thumbv7")
  set(ARM YES)
  set(THUMB YES)
  set(THUMBV7 YES)
endif()

if(DARWIN)
  set(CARCH ${ARCH})
  if (ARCH STREQUAL "x86")
    set(CARCH i386)
  elseif (ARCH STREQUAL "thumbv6")
    set(CARCH armv6)
  elseif (ARCH STREQUAL "thumbv7")
    set(CARCH armv7)
  endif()
endif()

if(IOS)
  if(NOT SYSROOT)
    if(ARCH STREQUAL "x86")
      set(SYSROOT "/Developer/Platforms/iPhoneSimulator.platform/Developer/SDKs/iPhoneSimulator5.0.sdk")
    else()
      set(SYSROOT "/Developer/Platforms/iPhoneOS.platform/Developer/SDKs/iPhoneOS5.0.sdk")
    endif()
  endif()
endif()

# Set up global C and C++ compiler flags
set(C_CXX_FLAGS "${C_CXX_FLAGS} -Wall -fvisibility=hidden -fdata-sections -ffunction-sections")
set(C_CXX_FLAGS "${C_CXX_FLAGS} -fno-omit-frame-pointer") # The unwind code relies on frame pointers being present.
set(C_CXX_FLAGS "${C_CXX_FLAGS} -fasynchronous-unwind-tables") # Generate eh frames for C code. Without it exceptions won't work.
if(CARCH)
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -arch ${CARCH}")
else()
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -m32")
endif()
if (ARCH STREQUAL "thumbv6" OR ARCH STREQUAL "thumbv7")
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -mthumb")
endif()
if(SYSROOT)
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -isysroot ${SYSROOT}")
endif()

# Linker flags
if(CARCH)
  set(LD_FLAGS "${LD_FLAGS} -arch ${CARCH}")
else()
  set(LD_FLAGS "${LD_FLAGS} -m32")
endif()
if(SYSROOT)
  set(LD_FLAGS "${LD_FLAGS} -isysroot ${SYSROOT} -miphoneos-version-min=3.0")
endif()
if(IOS)
  set(LD_FLAGS "${LD_FLAGS} -miphoneos-version-min=3.0")
endif()

set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${C_CXX_FLAGS} ${C_FLAGS}")
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${C_CXX_FLAGS} ${CXX_FLAGS}")
set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${LD_FLAGS}")
set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} ${LD_FLAGS}")
set(CMAKE_MODULE_LINKER_FLAGS "${CMAKE_MODULE_LINKER_FLAGS} ${LD_FLAGS}")

# Assembler flags
if(CARCH)
  set(ASM_FLAGS "${ASM_FLAGS} -c -arch ${CARCH}")
else()
  set(ASM_FLAGS "${ASM_FLAGS} -c -m32")
endif()
set(CMAKE_ASM_COMPILE_OBJECT "${CMAKE_C_COMPILER} ${ASM_FLAGS} -o <OBJECT> <SOURCE>")
enable_language(ASM)

if(LINUX)
  add_definitions(-D_GNU_SOURCE)
endif()

if(LINUX)
  set(HY_OS linux)
  add_definitions(-DLINUX)
elseif(DARWIN)
  if(MACOSX)
    add_definitions(-DMACOSX)
  else()
    add_definitions(-DIOS)
  endif()
  set(HY_OS macosx)
  add_definitions(-DDARWIN)
endif()

if (ARCH STREQUAL "x86")
  add_definitions(-DHYX86)
  add_definitions(-DRVM_X86)
elseif (ARCH STREQUAL "armv6")
  add_definitions(-DHYARM)
  add_definitions(-DRVM_ARMV6)
elseif (ARCH STREQUAL "armv7")
  add_definitions(-DHYARM)
  add_definitions(-DRVM_ARMV7)
elseif (ARCH STREQUAL "thumbv6")
  add_definitions(-DHYARM)
  add_definitions(-DRVM_THUMBV6)
elseif (ARCH STREQUAL "thumbv7")
  add_definitions(-DHYARM)
  add_definitions(-DRVM_THUMBV7)
endif()

set(INSTALL_DIR ${CMAKE_SOURCE_DIR}/binaries/${OS}/${ARCH}/${CMAKE_BUILD_TYPE})

message(STATUS "ARCH: ${ARCH}")
message(STATUS "OS: ${OS}")
message(STATUS "SYSROOT: ${SYSROOT}")
message(STATUS "CMAKE_BUILD_TYPE: ${CMAKE_BUILD_TYPE}")
message(STATUS "CMAKE_C_COMPILER: ${CMAKE_C_COMPILER}")
message(STATUS "CMAKE_CXX_COMPILER: ${CMAKE_CXX_COMPILER}")
message(STATUS "CMAKE_C_FLAGS: ${CMAKE_C_FLAGS}")
message(STATUS "CMAKE_CXX_FLAGS: ${CMAKE_CXX_FLAGS}")
message(STATUS "CMAKE_EXE_LINKER_FLAGS: ${CMAKE_EXE_LINKER_FLAGS}")
message(STATUS "CMAKE_SHARED_LINKER_FLAGS: ${CMAKE_SHARED_LINKER_FLAGS}")
message(STATUS "CMAKE_MODULE_LINKER_FLAGS: ${CMAKE_MODULE_LINKER_FLAGS}")

set(EXTGC_CFLAGS "${CMAKE_C_FLAGS}")
if(DARWIN)
  set(EXTGC_CFLAGS "${EXTGC_CFLAGS} -DNO_DYLD_BIND_FULLY_IMAGE")
  if(IOS)
    set(EXTGC_CFLAGS "${EXTGC_CFLAGS} -isysroot ${SYSROOT} -miphoneos-version-min=3.0")
    if (ARCH MATCHES ".*(thumb|arm).*")
      set(HOST --host=arm-apple-darwin7)
    endif()
  endif()
endif()
# TODO: Use CMake to build GC? There is a CMakeLists.txt but I cannot get the install to work.
ExternalProject_Add(extgc 
  URL ${CMAKE_SOURCE_DIR}/dependencies/bdwgc-20120327.tar.gz
  PATCH_COMMAND patch -d ../extgc -p1 < ${CMAKE_SOURCE_DIR}/dependencies/bdwgc.patch
  CONFIGURE_COMMAND bash -c "CC=${CMAKE_C_COMPILER} CFLAGS='${EXTGC_CFLAGS}' ../extgc/configure ${HOST} --enable-shared=no --prefix=${CMAKE_BINARY_DIR}/gc"
)
install(FILES ${CMAKE_BINARY_DIR}/gc/lib/libgc.a DESTINATION ${INSTALL_DIR})

add_subdirectory(core/src)
add_subdirectory(bc/src)
add_subdirectory(hyprt/src)
add_subdirectory(rt)

